<command>
<name>change ossi data clinic surgery action</name>
<description>List the surgery metadata defined for data clinic</description>
<type>Local Syntax</type>

<argument datatype="string" name="uc_data_surg_id">Surgery Id</argument>

<argument datatype="string" name="uc_data_surg_cmd_id_act"></argument>
<argument datatype="string" name="uc_data_surg_cmd_id_act_cmd"></argument>
<argument datatype="string" name="uc_data_surg_cmd_id_act_cl"></argument>

<argument datatype="string" name="uc_data_surg_action_dda_id"></argument>
<argument datatype="string" name="uc_data_surg_action_mls_text"></argument>
<argument datatype="string" name="uc_data_surg_action_mls_cl"></argument>

<argument datatype="string" name="uc_data_surg_cmd_id_act_ctl"></argument>


<local-syntax>
<![CDATA[
/*
 * Change Log
 * Initial Check In
 * Kent.Zhao - 10731
 */
publish data
where new_uc_data_surg_cmd_id_act_cmd = @uc_data_surg_cmd_id_act_cmd
and new_uc_data_surg_action_mls_text = @uc_data_surg_action_mls_text
and new_uc_data_surg_action_mls_cl = @uc_data_surg_action_mls_cl
and new_uc_data_surg_cmd_id_act_ctl = @uc_data_surg_cmd_id_act_ctl
and uc_force_inhibit_version_control = '1'
and locale_id = nvl(nvl(@locale_id, @@LOCALE_ID),'US_ENGLISH' )
|
{
    list ossi data clinic surgery actions
    catch (-1403,510)
    |
    if ( @? = 0 and @uc_data_surg_cmd_id_act )
    {
        if ( @new_uc_data_surg_cmd_id_act_cmd != @uc_data_surg_cmd_id_act_cmd )
        {
            change les command
            where les_cmd_id = @uc_data_surg_cmd_id_act
            and cust_lvl = @uc_data_surg_cmd_id_act_cl
            and syntax = @new_uc_data_surg_cmd_id_act_cmd
            |
            publish data
            where mod_uc_data_surg_cmd_id_act_cmd = '1'
        }
        |
        if ( @new_uc_data_surg_action_mls_text != @uc_data_surg_action_mls_text )
        {
            change system description
            where colnam = 'dda_id'
            and colval = @uc_data_surg_action_dda_id
            and locale_id = @locale_id
            and cust_lvl = @uc_data_surg_action_mls_cl
            and mls_text = @new_uc_data_surg_action_mls_text
            |
            publish data
            where mod_uc_data_surg_action_mls_text = '1'
        }
        |
        if ( @new_uc_data_surg_cmd_id_act_ctl != @uc_data_surg_cmd_id_act_ctl )
        {
            if ( @new_uc_data_surg_cmd_id_act_ctl != @uc_data_surg_cmd_id_act_ctl )
            {
                if ( @new_uc_data_surg_cmd_id_act_ctl = 1 )
                {
                    create option
                    where opt_nam = @uc_data_surg_cmd_id_act
                    and opt_typ = 'O'
                    and ena_flg = 1
                    and mls_text = @uc_data_surg_cmd_id_act
                    catch (-1,512)
                }
                else
                {
                    [
                    delete from les_opt_ath
                    where opt_nam = @uc_data_surg_cmd_id_act
                    ]
                    catch (-1403,510)
                    ;
                    remove option
                    where opt_nam = @uc_data_surg_cmd_id_act
                    catch (-1403,510)
                }
                ;
                publish data
                where mod_uc_data_surg_cmd_id_act_ctl = '1'
            } 
        }
        |
        publish data
        where uc_data_surg_id = @uc_data_surg_id
        and uc_data_surg_cmd_id_act = @uc_data_surg_cmd_id_act
        and mod_uc_data_surg_cmd_id_act_cmd = @mod_uc_data_surg_cmd_id_act_cmd
        and mod_uc_data_surg_action_mls_text = @mod_uc_data_surg_action_mls_text
        and mod_uc_data_surg_cmd_id_act_ctl = @mod_uc_data_surg_cmd_id_act_ctl
    }
}

]]>
</local-syntax>

<documentation>
<remarks>
Change the action information
</remarks>

<retrows>1</retrows>

<retcol name="uc_data_surg_id " type="COMTYP_CHAR">See command</retcol>
<retcol name="uc_data_surg_cmd_id_act " type="COMTYP_CHAR">See command</retcol>

<retcol name="mod_uc_data_surg_cmd_id_act_cmd " type="COMTYP_CHAR">See command</retcol>
<retcol name="mod_uc_data_surg_action_mls_text " type="COMTYP_CHAR">See command</retcol>
<retcol name="mod_uc_data_surg_cmd_id_act_ctl " type="COMTYP_CHAR">See command</retcol>



<exception value="eOK">The command completed successfully.</exception>

</documentation>


</command>